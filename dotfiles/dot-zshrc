# benchmark zsh
alias zsh-time="time zsh -i -c exit"
alias zsh-debug="time ZSH_DEBUG=1 zsh -i -c exit"
if [[ -n "$ZSH_DEBUG" ]]; then
  zmodload zsh/zprof
fi

# VS Code Shell Integration
if [[ -n "$VSCODE_SHELL_INTEGRATION" && -r "$VSCODE_SHELL_INTEGRATION" ]]; then
  source "$VSCODE_SHELL_INTEGRATION"
fi

# Avoid clobbering preexec/precmd hooks (important for integration + themes)
typeset -ga precmd_functions preexec_functions

# Auto-reset mouse reporting mode before each prompt
# Fixes stuck mouse mode when programs (vim, tmux, less) crash or don't clean up
_reset_mouse_mode() {
  printf '\e[?1000l\e[?1002l\e[?1003l\e[?1006l'
}
precmd_functions+=(_reset_mouse_mode)

# macOS Homebrew paths (only add if they exist)
if [[ -d /opt/homebrew/bin ]]; then
  export PATH=/opt/homebrew/bin:/opt/homebrew/opt/openjdk@17/bin:$PATH
fi

# You may need to manually set your language environment
export LANG=en_US.UTF-8

# Load environment variables from ~/.env
if [[ -f ~/.env ]]; then
    while IFS= read -r line || [[ -n "$line" ]]; do
        # Skip empty lines and comments (lines starting with #)
        if [[ -n "$line" && ! "$line" =~ ^[[:space:]]*# ]]; then
            export "$line"
        fi
    done < ~/.env
fi

# XDG-compliant tool paths
export HISTFILE="${XDG_STATE_HOME:-$HOME/.local/state}/zsh/history"
export CARGO_HOME="${XDG_DATA_HOME:-$HOME/.local/share}/cargo"
export RUSTUP_HOME="${XDG_DATA_HOME:-$HOME/.local/share}/rustup"
export NPM_CONFIG_CACHE="${XDG_CACHE_HOME:-$HOME/.cache}/npm"
export DOCKER_CONFIG="${XDG_CONFIG_HOME:-$HOME/.config}/docker"
export LESSHISTFILE="${XDG_CACHE_HOME:-$HOME/.cache}/less/history"
export GOPATH="${XDG_DATA_HOME:-$HOME/.local/share}/go"

# Path to your oh-my-zsh installation.
export ZSH="${XDG_DATA_HOME:-$HOME/.local/share}/oh-my-zsh"
# Fallback: use legacy path if XDG path doesn't exist yet
[[ ! -d "$ZSH" && -d "$HOME/.oh-my-zsh" ]] && export ZSH="$HOME/.oh-my-zsh"


# Performance: cache Windows USERPROFILE for Claude Code on WSL2
# Prevents repeated slow powershell.exe calls (~2.5s each, 7+ times per session)
# See: https://github.com/anthropics/claude-code/issues/27367
if [[ -z "$USERPROFILE" && -d /mnt/c ]]; then
  export USERPROFILE="C:\\Users\\rapha"
fi

# Plugin config

# tmux integration (manual start - use `tmux attach -t main || tmux new -s main`)
# Disabled auto-start to allow manual decision
# if [[ -z "$TMUX" && "$TERM_PROGRAM" != "vscode" && -z "$NO_TMUX" && "$CLAUDECODE" != "1" ]]; then
#   ZSH_TMUX_AUTOSTART=true
#   tmux attach -t main 2>/dev/null || tmux new -s main
# fi

# Which plugins would you like to load?
# Standard plugins can be found in $ZSH/plugins/
# Custom plugins may be added to $ZSH_CUSTOM/plugins/
# Example format: plugins=(rails git textmate ruby lighthouse)
# Add wisely, as too many plugins slow down shell startup.
plugins=(evalcache tmux zsh-syntax-highlighting zsh-autosuggestions)

source $ZSH/oh-my-zsh.sh
source ~/.config/zsh/aliases.zsh

# User configuration

# zsh-autosuggestions
ZSH_AUTOSUGGEST_STRATEGY=(history completion)

# This speeds up pasting w/ autosuggest
# https://github.com/zsh-users/zsh-autosuggestions/issues/238
autoload -Uz bracketed-paste-magic
zle -N bracketed-paste bracketed-paste-magic

# https://github.com/zsh-users/zsh-autosuggestions/issues/351
ZSH_AUTOSUGGEST_CLEAR_WIDGETS+=(bracketed-paste)
ZSH_AUTOSUGGEST_CLEAR_WIDGETS+=(backward-delete-char)
# Catppuccin Macchiato syntax highlighting
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main cursor)
typeset -A ZSH_HIGHLIGHT_STYLES
ZSH_HIGHLIGHT_STYLES[comment]='fg=#6e738d'
ZSH_HIGHLIGHT_STYLES[alias]='fg=#a6da95'
ZSH_HIGHLIGHT_STYLES[suffix-alias]='fg=#a6da95'
ZSH_HIGHLIGHT_STYLES[global-alias]='fg=#a6da95'
ZSH_HIGHLIGHT_STYLES[function]='fg=#8aadf4'
ZSH_HIGHLIGHT_STYLES[command]='fg=#8aadf4'
ZSH_HIGHLIGHT_STYLES[precommand]='fg=#8aadf4,italic'
ZSH_HIGHLIGHT_STYLES[autodirectory]='fg=#f5a97f,italic'
ZSH_HIGHLIGHT_STYLES[single-hyphen-option]='fg=#f5a97f'
ZSH_HIGHLIGHT_STYLES[double-hyphen-option]='fg=#f5a97f'
ZSH_HIGHLIGHT_STYLES[back-quoted-argument]='fg=#c6a0f6'
ZSH_HIGHLIGHT_STYLES[builtin]='fg=#ed8796'
ZSH_HIGHLIGHT_STYLES[reserved-word]='fg=#ed8796'
ZSH_HIGHLIGHT_STYLES[single-quoted-argument]='fg=#a6da95'
ZSH_HIGHLIGHT_STYLES[double-quoted-argument]='fg=#a6da95'
ZSH_HIGHLIGHT_STYLES[dollar-quoted-argument]='fg=#a6da95'
ZSH_HIGHLIGHT_STYLES[rc-quote]='fg=#8bd5ca'
ZSH_HIGHLIGHT_STYLES[dollar-double-quoted-argument]='fg=#8bd5ca'
ZSH_HIGHLIGHT_STYLES[back-double-quoted-argument]='fg=#8bd5ca'
ZSH_HIGHLIGHT_STYLES[back-dollar-quoted-argument]='fg=#8bd5ca'
ZSH_HIGHLIGHT_STYLES[assign]='fg=#cad3f5'
ZSH_HIGHLIGHT_STYLES[redirection]='fg=#cad3f5'
ZSH_HIGHLIGHT_STYLES[globbing]='fg=#cad3f5'
ZSH_HIGHLIGHT_STYLES[commandseparator]='fg=#ed8796'
ZSH_HIGHLIGHT_STYLES[command-substitution-delimiter]='fg=#cad3f5'
ZSH_HIGHLIGHT_STYLES[command-substitution-delimiter-unquoted]='fg=#cad3f5'
ZSH_HIGHLIGHT_STYLES[command-substitution-delimiter-quoted]='fg=#a6da95'
ZSH_HIGHLIGHT_STYLES[process-substitution-delimiter]='fg=#cad3f5'
ZSH_HIGHLIGHT_STYLES[back-quoted-argument-unclosed]='fg=#ed8796'
ZSH_HIGHLIGHT_STYLES[single-quoted-argument-unclosed]='fg=#ed8796'
ZSH_HIGHLIGHT_STYLES[double-quoted-argument-unclosed]='fg=#ed8796'
ZSH_HIGHLIGHT_STYLES[dollar-quoted-argument-unclosed]='fg=#ed8796'
ZSH_HIGHLIGHT_STYLES[path]=none
ZSH_HIGHLIGHT_STYLES[path_prefix]=none

# workaround for autosuggest not clearing on enter
# https://github.com/zsh-users/zsh-autosuggestions/issues/525
# magic-enter() {
# 	if [[ -z "$BUFFER" ]]; then
# 		zle clear-screen
# 	else
# 		# Call built-in accept-line
# 		zle .accept-line
# 	fi
# }
# zle -N accept-line magic-enter
# Turn off autocomplete beeps
unsetopt LIST_BEEP

# zsh reduce esc key delay
# https://www.johnhawthorn.com/2012/09/vi-escape-delays/
# 10ms for key sequences
KEYTIMEOUT=1

# zsh history
HISTSIZE=1000000
SAVEHIST=1000000
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_SAVE_NO_DUPS
setopt HIST_FIND_NO_DUPS
setopt HIST_REDUCE_BLANKS
setopt EXTENDED_HISTORY
setopt SHARE_HISTORY

# keybindings
bindkey '^f' edit-command-line

# workaround for ls colors
# setup for bsd ls
export LSCOLORS='ExGxFxdaCxDaDahbadacec'
# setup for gnu ls
export LS_COLORS='rs=0:di=01;34:ln=01;36:mh=00:pi=40;33:so=01;35:do=01;35:bd=40;33;01:cd=40;33;01:or=40;31;01:mi=00:su=37;41:sg=30;43:ca=30;41:tw=30;42:ow=34;42:st=37;44:ex=01;32:*.tar=01;31:*.tgz=01;31:*.arc=01;31:*.arj=01;31:*.taz=01;31:*.lha=01;31:*.lz4=01;31:*.lzh=01;31:*.lzma=01;31:*.tlz=01;31:*.txz=01;31:*.tzo=01;31:*.t7z=01;31:*.zip=01;31:*.z=01;31:*.dz=01;31:*.gz=01;31:*.lrz=01;31:*.lz=01;31:*.lzo=01;31:*.xz=01;31:*.zst=01;31:*.tzst=01;31:*.bz2=01;31:*.bz=01;31:*.tbz=01;31:*.tbz2=01;31:*.tz=01;31:*.deb=01;31:*.rpm=01;31:*.jar=01;31:*.war=01;31:*.ear=01;31:*.sar=01;31:*.rar=01;31:*.alz=01;31:*.ace=01;31:*.zoo=01;31:*.cpio=01;31:*.7z=01;31:*.rz=01;31:*.cab=01;31:*.wim=01;31:*.swm=01;31:*.dwm=01;31:*.esd=01;31:*.jpg=01;35:*.jpeg=01;35:*.mjpg=01;35:*.mjpeg=01;35:*.gif=01;35:*.bmp=01;35:*.pbm=01;35:*.pgm=01;35:*.ppm=01;35:*.tga=01;35:*.xbm=01;35:*.xpm=01;35:*.tif=01;35:*.tiff=01;35:*.png=01;35:*.svg=01;35:*.svgz=01;35:*.mng=01;35:*.pcx=01;35:*.mov=01;35:*.mpg=01;35:*.mpeg=01;35:*.m2v=01;35:*.mkv=01;35:*.webm=01;35:*.webp=01;35:*.ogm=01;35:*.mp4=01;35:*.m4v=01;35:*.mp4v=01;35:*.vob=01;35:*.qt=01;35:*.nuv=01;35:*.wmv=01;35:*.asf=01;35:*.rm=01;35:*.rmvb=01;35:*.flc=01;35:*.avi=01;35:*.fli=01;35:*.flv=01;35:*.gl=01;35:*.dl=01;35:*.xcf=01;35:*.xwd=01;35:*.yuv=01;35:*.cgm=01;35:*.emf=01;35:*.ogv=01;35:*.ogx=01;35:*.aac=00;36:*.au=00;36:*.flac=00;36:*.m4a=00;36:*.mid=00;36:*.midi=00;36:*.mka=00;36:*.mp3=00;36:*.mpc=00;36:*.ogg=00;36:*.ra=00;36:*.wav=00;36:*.oga=00;36:*.opus=00;36:*.spx=00;36:*.xspf=00;36:';
# workaround for wsl folder highlights
export LS_COLORS='ow=1;34:'$LS_COLORS
# setup for zsh tab complete colors
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"

# default terminal editor
if type nvim > /dev/null
then
  export EDITOR='nvim'
fi

# Path configuration
# If $NVIM variable is not set then update the path variable
if [[ -z $NVIM ]]
then
  # add ~/.local/bin to the path (includes Claude Code)
  export PATH=$HOME/.local/bin:$PATH
  # add cargo bin to path
  [[ -d "${CARGO_HOME:-$HOME/.cargo}/bin" ]] && export PATH="${CARGO_HOME:-$HOME/.cargo}/bin:$PATH"
fi

# java: jenv
if [[ -d "$HOME/.jenv" ]]; then
  jenv() {
    unset -f jenv
    export PATH="$HOME/.jenv/bin:$PATH"
    _evalcache jenv init -
    jenv "$@"
  }
fi

# python: pyenv
if [[ -d "$HOME/.pyenv" ]]; then
  pyenv() {
    unset -f pyenv
    export PYENV_ROOT="$HOME/.pyenv"
    [[ -d $PYENV_ROOT/bin ]] && export PATH="$PYENV_ROOT/bin:$PATH"
    _evalcache pyenv init -
    pyenv "$@"
  }
fi

# node: nvm
export NVM_DIR="${XDG_DATA_HOME:-$HOME/.local/share}/nvm"
# Fallback to legacy paths
if [[ ! -d "$NVM_DIR" ]]; then
  [[ -d "$HOME/.nvm" ]] && export NVM_DIR="$HOME/.nvm"
  [[ -d "$HOME/.config/nvm" ]] && export NVM_DIR="$HOME/.config/nvm"
fi

# Unset NPM_CONFIG_PREFIX if invalid (causes nvm errors in sandboxed environments)
if [[ -n "$NPM_CONFIG_PREFIX" && ! -d "$NPM_CONFIG_PREFIX" ]]; then
  unset NPM_CONFIG_PREFIX
fi

if [[ -d "$NVM_DIR" || -d "/opt/homebrew/opt/nvm" ]]; then
  # Add node/npm to PATH without loading nvm (use default version)
  if [[ -d "$NVM_DIR/versions/node" ]]; then
    local default_node_dir="$NVM_DIR/versions/node"
    local default_node=$(ls -1 "$default_node_dir" 2>/dev/null | sort -V | tail -1)
    [[ -n "$default_node" ]] && export PATH="$default_node_dir/$default_node/bin:$PATH"
  elif [[ -d "/opt/homebrew/opt/nvm/versions/node" ]]; then
    local default_node_dir="/opt/homebrew/opt/nvm/versions/node"
    local default_node=$(ls -1 "$default_node_dir" 2>/dev/null | sort -V | tail -1)
    [[ -n "$default_node" ]] && export PATH="$default_node_dir/$default_node/bin:$PATH"
  fi

  # Lazy-load nvm on first use
  nvm() {
    unset -f nvm node npm npx
    if [[ -s "/opt/homebrew/opt/nvm/nvm.sh" ]]; then
      export NVM_DIR="/opt/homebrew/opt/nvm"
      \. "/opt/homebrew/opt/nvm/nvm.sh"
      [ -s "/opt/homebrew/opt/nvm/bash_completion" ] && \. "/opt/homebrew/opt/nvm/bash_completion"
    elif [[ -s "$NVM_DIR/nvm.sh" ]]; then
      \. "$NVM_DIR/nvm.sh"
      [ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"
    fi
    nvm "$@"
  }
  # Lazy-load node/npm/npx too
  node() { unset -f node; nvm use --lts --silent 2>/dev/null; node "$@"; }
  npm() { unset -f npm; nvm use --lts --silent 2>/dev/null; npm "$@"; }
  npx() { unset -f npx; nvm use --lts --silent 2>/dev/null; npx "$@"; }
fi

if [[ -d  "/mnt/c/Users/rapha/AppData/Local/Programs/Microsoft VS Code/bin" ]]; then
  export PATH=$PATH:"/mnt/c/Users/rapha/AppData/Local/Programs/Microsoft VS Code/bin"
fi

# Fix for Cypress - unset ELECTRON_RUN_AS_NODE to prevent Cypress from failing
# https://github.com/cypress-io/cypress/issues/5446
unset ELECTRON_RUN_AS_NODE

# zoxide
if type zoxide > /dev/null; then
  if [[ "$CLAUDECODE" != "1" ]]; then
    _evalcache zoxide init zsh --cmd cd
  fi
fi

# eza
if type eza > /dev/null; then
  alias ls="eza -g -s Name --group-directories-first --time-style long-iso --icons=auto"
  alias l="ls -la"
  alias la="ls -la -a"
  alias ll="ls -l"
fi

# fzf
if type fzf > /dev/null; then
  export FZF_DEFAULT_OPTS=" \
  --color=bg+:#363a4f,bg:#24273a,spinner:#f4dbd6,hl:#ed8796 \
  --color=fg:#cad3f5,header:#ed8796,info:#c6a0f6,pointer:#f4dbd6 \
  --color=marker:#b7bdf8,fg+:#cad3f5,prompt:#c6a0f6,hl+:#ed8796 \
  --color=selected-bg:#494d64 \
  --multi"
  [ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
  alias fzf="fzf --ansi"
fi

# bat
if type bat > /dev/null; then
  alias cat="bat --style=plain --paging=auto"

  export MANPAGER="sh -c 'col -bx | bat -l man -p'"
  export MANROFFOPT="-c"

  help() {
    "$@" --help 2>&1 | bat --plain --language=help
  }
fi

# lazygit
if type lazygit > /dev/null; then
  # this is a fix for not showing the correct colors using delta
  export COLORTERM=truecolor
  # workaround for true colors in lazygit from tmux
  # https://github.com/jesseduffield/lazygit/issues/3668
  alias lazygit='env TERM=screen-256color lazygit'
fi

# ghostty: fix 'xterm-ghostty': unknown terminal type on remote servers
if [[ "$TERM" == "xterm-ghostty" ]]; then
  alias ssh='TERM=xterm-256color ssh'
fi

# rclone: decrypt config via macOS Keychain
if type rclone &>/dev/null; then
  export RCLONE_PASSWORD_COMMAND="security find-generic-password -a rclone -s rclone-config -w"
fi


# Ensure unique PATH entries using zsh's unique array feature
typeset -U path PATH

if [[ -n "$ZSH_DEBUG" ]]; then
  zprof
fi

export STARSHIP_CONFIG=~/.config/starship/starship.toml
if type starship &>/dev/null; then
  _evalcache starship init zsh
fi

# Aliases (already sourced earlier on line 61)

if [ -d "/opt/homebrew/opt/ruby/bin" ]; then
  export PATH=/opt/homebrew/opt/ruby/bin:$PATH
  export PATH=`gem environment gemdir`/bin:$PATH
fi

if [ -f /opt/homebrew/opt/chruby/share/chruby/chruby.sh ]; then
    source /opt/homebrew/opt/chruby/share/chruby/chruby.sh
    source /opt/homebrew/opt/chruby/share/chruby/auto.sh
elif [ -f /usr/share/chruby/chruby.sh ]; then
    source /usr/share/chruby/chruby.sh
    source /usr/share/chruby/auto.sh
fi

# The following lines have been added by Docker Desktop to enable Docker CLI completions.
fpath=($HOME/.docker/completions $fpath)
# Guard against double-running compinit (oh-my-zsh already does this)
type compdef >/dev/null 2>&1 || { autoload -Uz compinit && compinit; }
# End of Docker CLI completions

# bun completions
[ -s "/Users/raphael/.bun/_bun" ] && source "/Users/raphael/.bun/_bun"

# bun
export BUN_INSTALL="$HOME/.bun"
export PATH="$BUN_INSTALL/bin:$PATH"

# vscode shell integration
if [[ "$TERM_PROGRAM" == "vscode" ]]; then
  # macOS
  if [[ -f "/Applications/Visual Studio Code.app/Contents/Resources/app/out/vs/workbench/contrib/terminal/common/scripts/shellIntegration-rc.zsh" ]]; then
    . "/Applications/Visual Studio Code.app/Contents/Resources/app/out/vs/workbench/contrib/terminal/common/scripts/shellIntegration-rc.zsh"
  # Linux (code installed via package manager)
  elif [[ -f "/usr/share/code/resources/app/out/vs/workbench/contrib/terminal/common/scripts/shellIntegration-rc.zsh" ]]; then
    . "/usr/share/code/resources/app/out/vs/workbench/contrib/terminal/common/scripts/shellIntegration-rc.zsh"
  fi
fi

fpath+=~/.zfunc
